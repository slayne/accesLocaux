module GestAcces {
	typedef string Photo;
	typedef short Heure;

	struct Jour {
	    short annee;
	    short mois;
	    short jour;
	};

    struct Date {
        Jour j;
        Heure h;
        short m;
    };

	struct Zone {
	    short idZone;
	    string nomZone;
	};

	struct Log {
	    Date date;
	    string log;
	};



    struct AccesCorba {
        short idAcces;
        Zone zone;
        short idCollaborateur;
        Date dateDebut;
        Date dateFin;
        short heureDebut;
        short heureFin;
        boolean isTemp;
     };

     typedef sequence<AccesCorba> lAcces;

     struct CollaborateurCorba {
                 short id;
                 string nom;
                 Photo photo;
                 Date dateEntree;
                 string empreinte;
                 boolean isTemp;
                 Date dateFin;
                 lAcces acces;
         };

    typedef sequence<CollaborateurCorba> lCollabo;

	interface ServeurAnnuaire {
		exception CollaborateurInexistant {};
		exception CollaborateurDejaExistant {};

		boolean demanderAcces(in string Photo, in string mdp, in short idZone) raises (CollaborateurInexistant);
		void enregisterCollaborateurPermanent(in string nom, in Photo p, in string empreinte, in Date de) raises(CollaborateurDejaExistant);
		void enregisterCollaborateurTemporaire(in string nom, in Photo p, in string empreinte, in Date de, in Date df) raises(CollaborateurDejaExistant);
		lCollabo rechercherCollaborateurs();
		void supprimerCollaborateur(in short id) raises(CollaborateurInexistant);
		CollaborateurCorba rechercherCollaborateur(in Photo p, in string empreinte) raises (CollaborateurInexistant);
	};

	interface ServeurEmpreinte {
		exception EmpreinteInexistante {};
		exception EmpreintePresente{};
		exception EmpreinteInvalide{};

		void enregistrerEmpreinte(in short idCollaborateur, in string empreinte) raises(EmpreintePresente);
		void modifierEmpreinte(in short idCollaborateur, in string nouvelleEmpreinte) raises (EmpreinteInexistante);
		void supprimerEmpreinte(in short idCollaborateur) raises (EmpreinteInexistante);
		void verifierEmpreinte(in short idCollaborateur,in string mdp) raises (EmpreinteInvalide);
		string getEmpreinte(in short idCollaborateur) raises (EmpreinteInvalide);
	};

    typedef sequence<Log> lLog;

	interface ServeurLog {
		oneway void envoyerLog(in string log);
		lLog afficherLogs();
		lLog afficherLogsFromDate(in Date date);
		lLog afficherLogsfromDateToDate(in Date dated, in Date datef);
	};

	typedef sequence<Zone> lZone;



	interface ServeurAcces {
		exception CollaborateurDejaExistant {};
		exception CollaborateurInexistant {};
		exception PorteInexistante {};
		exception ZoneInexistante {};

		void ajoutPerm(in short id, in Heure heureDebut, in Heure heureFin, in short idZone) raises (ZoneInexistante);
		void ajoutTemp(in short id, in Jour jourDeb, in Jour jourFin,in Heure heureDebut, in Heure heureFin, in short idZone) raises (ZoneInexistante);
		void supprimerUnAcces (in short idCollaborateur, in short idZone) raises (CollaborateurInexistant);
		boolean verifierAcces (in short idCollaborateur,in short idZone);
		void supprimerAccesCollaborateur(in short idCollaborateur) raises (CollaborateurInexistant);

		lZone getListeZone();

		// renvoie les zones liées à une porte
		lZone getZonesPorte(in short idPorte) raises (PorteInexistante);

		lAcces getAccesCollaborateur(in short idCollaborateur);
	};
};